/*
 * Copyright 2010 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.optaplanner.examples.conferencescheduling.solver;
    dialect "java"

import org.optaplanner.core.api.score.buildin.hardsoft.HardSoftScoreHolder;

import org.optaplanner.examples.conferencescheduling.domain.ConferenceParametrization;
import org.optaplanner.examples.conferencescheduling.domain.Room;
import org.optaplanner.examples.conferencescheduling.domain.Speaker;
import org.optaplanner.examples.conferencescheduling.domain.Talk;
import org.optaplanner.examples.conferencescheduling.domain.Timeslot;

global HardSoftScoreHolder scoreHolder;

// ############################################################################
// Hard constraints
// ############################################################################

rule "Talk type of timeslot"
    when
        Talk(timeslot != null, !getTalkType().equals(getTimeslot().getTalkType()))
    then
        scoreHolder.addHardConstraintMatch(kcontext, -100);
end

rule "Room unavailable timeslot"
    when
        Talk(hasUnavailableRoom())
    then
        scoreHolder.addHardConstraintMatch(kcontext, -10);
end

rule "Room conflict"
    when
        Talk(timeslot != null, $timeslot : timeslot,
                room != null, $room : room,
                $leftId : id)
        Talk(timeslot != null, getTimeslot().overlaps($timeslot),
                room == $room,
                id > $leftId)
    then
        scoreHolder.addHardConstraintMatch(kcontext, -10);
end

rule "Speaker unavailable timeslot"
    when
        Talk(hasAnyUnavailableSpeaker())
    then
        scoreHolder.addHardConstraintMatch(kcontext, -1);
end

rule "Speaker conflict"
    when
        $speaker : Speaker()
        Talk(hasSpeaker($speaker),
                timeslot != null, $timeslot : timeslot,
                $leftId : id)
        Talk(hasSpeaker($speaker),
                timeslot != null, getTimeslot().overlaps($timeslot),
                id > $leftId)
    then
        scoreHolder.addHardConstraintMatch(kcontext, -1);
end

rule "Speaker required timeslot tag"
    when
        Talk($count : missingSpeakerRequiredTimeslotTagCount() > 0)
    then
        scoreHolder.addHardConstraintMatch(kcontext, - $count);
end

rule "Speaker prohibited timeslot tag"
    when
        Talk($count : prevailingSpeakerProhibitedTimeslotTagCount() > 0)
    then
        scoreHolder.addHardConstraintMatch(kcontext, - $count);
end

rule "Talk required timeslot tag"
    when
        Talk($count : missingRequiredTimeslotTagCount() > 0)
    then
        scoreHolder.addHardConstraintMatch(kcontext, - $count);
end

rule "Talk prohibited timeslot tag"
    when
        Talk($count : prevailingProhibitedTimeslotTagCount() > 0)
    then
        scoreHolder.addHardConstraintMatch(kcontext, - $count);
end

rule "Speaker required room tag"
    when
        Talk($count : missingSpeakerRequiredRoomTagCount() > 0)
    then
        scoreHolder.addHardConstraintMatch(kcontext, - $count);
end

rule "Speaker prohibited room tag"
    when
        Talk($count : prevailingSpeakerProhibitedRoomTagCount() > 0)
    then
        scoreHolder.addHardConstraintMatch(kcontext, - $count);
end

rule "Talk required room tag"
    when
        Talk($count : missingRequiredRoomTagCount() > 0)
    then
        scoreHolder.addHardConstraintMatch(kcontext, - $count);
end

rule "Talk prohibited room tag"
    when
        Talk($count : prevailingProhibitedRoomTagCount() > 0)
    then
        scoreHolder.addHardConstraintMatch(kcontext, - $count);
end

// ############################################################################
// Soft constraints
// ############################################################################

rule "Theme track conflict"
    when
        ConferenceParametrization($weight : themeTrackConflict != 0)
        $leftTalk : Talk(timeslot != null, $timeslot : timeslot,
                $leftId : id)
        $rightTalk : Talk(timeslot != null,
                overlappingThemeTrackCount($leftTalk) > 0,
                getTimeslot().overlaps($timeslot),
                id > $leftId)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight * $rightTalk.overlappingThemeTrackCount($leftTalk));
end

rule "Sector conflict"
    when
        ConferenceParametrization($weight : sectorConflict != 0)
        $leftTalk : Talk(timeslot != null, $timeslot : timeslot,
                $leftId : id)
        $rightTalk : Talk(timeslot != null,
                overlappingSectorCount($leftTalk) > 0,
                getTimeslot().overlaps($timeslot),
                id > $leftId)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight * $rightTalk.overlappingSectorCount($leftTalk));
end

rule "Content audience level flow violation"
    when
        ConferenceParametrization($weight : contentAudienceLevelFlowViolation != 0)
        $leftTalk : Talk(timeslot != null, $timeslot : timeslot,
                $audienceLevel : audienceLevel)
        $rightTalk : Talk(timeslot != null,
                overlappingContentCount($leftTalk) > 0,
                audienceLevel < $audienceLevel,
                !timeslot.endsBefore($timeslot))
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight * $rightTalk.overlappingContentCount($leftTalk));
end

rule "Audience level diversity"
    when
        ConferenceParametrization($weight : audienceLevelDiversity != 0)
        Talk(timeslot != null, $timeslot : timeslot,
                $audienceLevel : audienceLevel,
                $leftId : id)
        // Timeslot.overlaps() is deliberately not used
        Talk(timeslot == $timeslot,
                audienceLevel != $audienceLevel,
                id > $leftId)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, $weight);
end

rule "Language diversity"
    when
        ConferenceParametrization($weight : languageDiversity != 0)
        Talk(timeslot != null, $timeslot : timeslot,
                $language : language,
                $leftId : id)
        // Timeslot.overlaps() is deliberately not used
        Talk(timeslot == $timeslot,
                language != $language,
                id > $leftId)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, $weight);
end

rule "Speaker preferred timeslot tag"
    when
        ConferenceParametrization($weight : speakerPreferredTimeslotTag != 0)
        Talk($count : missingSpeakerPreferredTimeslotTagCount() > 0)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight * $count);
end

rule "Talk preferred timeslot tag"
    when
        ConferenceParametrization($weight : talkPreferredTimeslotTag != 0)
        Talk($count : missingPreferredTimeslotTagCount() > 0)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight * $count);
end

rule "Speaker undesired timeslot tag"
    when
        ConferenceParametrization($weight : speakerUndesiredTimeslotTag != 0)
        Talk($count : prevailingSpeakerUndesiredTimeslotTagCount() > 0)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight * $count);
end

rule "Talk undesired timeslot tag"
    when
        ConferenceParametrization($weight : talkUndesiredTimeslotTag != 0)
        Talk($count : prevailingUndesiredTimeslotTagCount() > 0)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight * $count);
end

rule "Speaker preferred room tag"
    when
        ConferenceParametrization($weight : speakerPreferredRoomTag != 0)
        Talk($count : missingSpeakerPreferredRoomTagCount() > 0)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight * $count);
end

rule "Talk preferred room tag"
    when
        ConferenceParametrization($weight : talkPreferredRoomTag != 0)
        Talk($count : missingPreferredRoomTagCount() > 0)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight * $count);
end

rule "Speaker undesired room tag"
    when
        ConferenceParametrization($weight : speakerUndesiredRoomTag != 0)
        Talk($count : prevailingSpeakerUndesiredRoomTagCount() > 0)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight * $count);
end

rule "Talk undesired room tag"
    when
        ConferenceParametrization($weight : talkUndesiredRoomTag != 0)
        Talk($count : prevailingUndesiredRoomTagCount() > 0)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $weight * $count);
end
